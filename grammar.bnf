<whitespace> ::= " "
<assignment_operator> ::= "="
<escape_character> ::= "\\"
<double_quotes> ::= "\""
<single_quote> ::= "'"
<punctuation> ::= "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ";" | "<"  | ">" | "?" | "@" | "[" | ":" |  "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~"
					| ":" 

<letter> ::= [a-z] | [A-Z]
<number> ::= [0-9] 

<whitespaces> ::= <whitespace> | <whitespace> <whitespaces>

<valid_character> ::= <number> | <letter> | <punctuation>

<any_character> ::= <valid_character> | <assignment_operator> | <escape_character> | <double_quotes> | <single_quote>

<character> ::= <valid_character> | <escape_character> <any_character>
<characters> ::= <character> | <character> <characters>

<double_quoted_character> ::= <valid_character> | <assignment_operator> | <single_quote> | <escape_character> <any_character> | <whitespace>
<double_quoted_characters>  ::= <double_quoted_character> | <double_quoted_character> <double_quoted_characters>

<single_quoted_character> ::= <valid_character> | <assignment_operator> | <double_quotes> | <escape_character> <any_character> | <whitespace>
<single_quoted_characters> ::= <single_quoted_character> | <single_quoted_character> <single_quoted_characters>

<unquoted_value> ::= <characters>
<double_quoted_value> ::= <double_quotes> <double_quoted_characters> <double_quotes>
<single_quoted_value> ::= <single_quote> <single_quoted_characters> <single_quote>

<arg> ::= <unquoted_value> | <double_quoted_value> | <single_quoted_value>

<args> ::= <arg> | <arg> <whitespaces> <args>


<kwarg> ::= <arg> <whitespaces> <assignment_operator> <whitespaces> <arg>
		| <arg> <assignment_operator> <whitespaces> <arg>
        | <arg> <whitespaces> <assignment_operator> <arg>
        | <arg> <assignment_operator> <arg>
        
<kwargs> ::= <kwarg> | <kwarg> <whitespaces> <kwargs>

<command> ::= <args>
		| <args> <whitespaces> <kwargs>
        | <kwargs>